allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:26.0.1'
    }

    compileJava {
        options.encoding = 'UTF-8'
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
        withSourcesJar()
        withJavadocJar()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.java
            }
        }
    }
}

subprojects {
    dependencies {
        implementation rootProject
    }
}

dependencies {
    testImplementation(platform('org.junit:junit-bom:5.11.4'))
    testImplementation 'org.junit.jupiter:junit-jupiter'

    testImplementation 'org.jetbrains:annotations:26.0.1'
}

test {
    useJUnitPlatform()
}

def javadocProjects= [
        ':',
        ':broker:broker-activemq',
        ':broker:broker-kafka',
        ':broker:broker-nats',
        ':broker:broker-postgresql',
        ':broker:broker-postgresql-hikari',
        ':broker:broker-rabbitmq',
        ':broker:broker-redis',
        ':broker:broker-sql',
        ':broker:broker-sql-hikari',
        ':module:module-sql',
        ':extension:extension-caffeine',
        ':extension:extension-guava',
        ':extension:extension-log4j',
        ':extension:extension-slf4j'
]

tasks.register('allJavadoc', Javadoc) {
    source javadocProjects.collect { project(it).sourceSets.main.allJava }
    classpath = files(javadocProjects.collect { project(it).sourceSets.main.compileClasspath })
    options {
        // Java
        links 'https://docs.oracle.com/en/java/javase/11/docs/api/'

        // Broker
        links 'https://javadoc.io/doc/org.apache.activemq/activemq-client/latest/'
        links 'https://javadoc.io/doc/org.apache.kafka/kafka-clients/latest/'
        links 'https://javadoc.io/doc/io.nats/jnats/latest/'
        links 'https://javadoc.io/doc/org.postgresql/postgresql/latest/'
        links 'https://rabbitmq.github.io/rabbitmq-java-client/api/current/'
        links 'https://javadoc.io/doc/redis.clients/jedis/latest/'
        links 'https://javadoc.io/doc/com.zaxxer/HikariCP/latest/'

        // Extension
        links 'https://javadoc.io/doc/com.github.ben-manes.caffeine/caffeine/latest/'
        links 'https://javadoc.io/doc/com.google.guava/guava/latest/'
        links 'https://logging.apache.org/log4j/2.x/javadoc/log4j-api/'
        links 'https://slf4j.org/api/'

        // Common
        links 'https://javadoc.io/doc/org.jetbrains/annotations/latest/'
    }
    destinationDir = file(project.layout.buildDirectory.dir('docs/all-javadoc'))
}
